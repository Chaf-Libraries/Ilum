#version 460
#extension GL_EXT_ray_tracing : enable
#extension GL_EXT_nonuniform_qualifier : enable
#extension GL_GOOGLE_include_directive : enable

#include "../RayTracing.glsl"
//#include "../Material.glsl"

layout(push_constant) uniform PushBlock
{
	uint anti_alias;
	uint directional_light_count;
	uint spot_light_count;
	uint point_light_count;
	int max_bounce;
	float parameter;
}
push_data;

layout(location = 0) rayPayloadEXT RayPayload ray_payload;
layout(location = 1) rayPayloadEXT ShadowPayload shadow_payload;

bool Unoccluded(Interaction from, vec3 dir, float dist)
{
	shadow_payload.visibility = false;
	Ray shadow_ray;
	shadow_ray.direction = normalize(dir);
	shadow_ray.origin    = OffsetRay(from.position, dot(shadow_ray.direction, from.normal) > 0 ? from.normal : -from.normal);
	shadow_ray.tmin      = 0.0;
	shadow_ray.tmax      = dist;

	// Trace shadow ray
	traceRayEXT(topLevelAS,                                                                                                            // acceleration structure
	            gl_RayFlagsTerminateOnFirstHitEXT | gl_RayFlagsOpaqueEXT | gl_RayFlagsSkipClosestHitShaderEXT | gl_RayFlagsCullBackFacingTrianglesEXT,        // rayFlags
	            0xFF,                                                                                                                  // cullMask
	            0,                                                                                                                     // sbtRecordOffset
	            0,                                                                                                                     // sbtRecordStride
	            1,                                                                                                                     // missIndex
	            shadow_ray.origin,                                                                                                     // ray origin
	            shadow_ray.tmin,                                                                                                       // ray min range
	            shadow_ray.direction,                                                                                                  // ray direction
	            shadow_ray.tmax,                                                                                                       // ray max range
	            1                                                                                                                      // payload (location = 1)
	);

	return shadow_payload.visibility;
}

bool IntersectScene(Ray ray)
{
	// Spawn a ray
	ray_payload.hitT = Infinity;
	traceRayEXT(topLevelAS,   // acceleration structure
			gl_RayFlagsCullBackFacingTrianglesEXT,     // rayFlags
			0xFF,         // cullMask
			0,            // sbtRecordOffset
			0,            // sbtRecordStride
			0,            // missIndex
			ray.origin,     // ray origin
			ray.tmin,          // ray min range
			ray.direction,  // ray direction
			ray.tmax,     // ray max range
			0             // payload (location = 0)
	);

	return ray_payload.hitT != Infinity;
}

void main() 
{
	const vec2 pixelCenter = vec2(gl_LaunchIDEXT.xy) + vec2(0.5);
	ray_payload.seed = init_random(gl_LaunchSizeEXT.xy, gl_LaunchIDEXT.xy, camera_data.frame_num);

	vec2 jitter = vec2(0.0);

	if(push_data.anti_alias == 1)
	{
		jitter = rand2(ray_payload.seed) - vec2(0.5);
	}

	const vec2 screen_coords = (pixelCenter + jitter) / vec2(gl_LaunchSizeEXT.xy) * 2.0 - 1.0;

	Ray ray = CameraCastRay(camera_data, screen_coords);

	vec3 radiance = vec3(0.0);
	vec3 throughout = vec3(1.0);
	vec3 absorption = vec3(0.0);

	for(int depth = 0; depth < push_data.max_bounce; depth++)
	{
		// Hit environment
		if(!IntersectScene(ray))
		{
			radiance += texture(Skybox, ray.direction).rgb;
			break;
		}

		ShadeState sstate = GetShadeState(ray_payload);

		Interaction interaction;
		interaction.position = sstate.position;
		interaction.normal = sstate.normal;
		interaction.tangent = sstate.tangent_u;
		interaction.bitangent = sstate.tangent_v;
		interaction.texCoord = sstate.tex_coord;
		interaction.matID = sstate.matIndex;
		interaction.ffnormal       = dot(interaction.normal, ray.direction) <= 0.0 ? interaction.normal : -interaction.normal;

		GetMaterial(interaction, ray);

		vec3 n = interaction.ffnormal;
		vec3 wo = -ray.direction;

		wo = normalize(WorldToLocal(wo, interaction.ffnormal, interaction.tangent, interaction.bitangent));

		// Add contribution of each point light source
		for(uint i = 0; i < push_data.point_light_count; i++)
		{
			vec3 wi;
			float pdf;
			vec3 Li = Sample_Li(point_lights[i], interaction, rand2(ray_payload.seed), wi, pdf);
			if(Li == vec3(0.0) || pdf == 0)
			{
				continue;
			}

			vec3 dir = normalize(point_lights[i].position - interaction.position);
			float dist = length(point_lights[i].position - interaction.position);

			if(Li != vec3(0.0) && Unoccluded(interaction, dir, dist))
			{
				wi = normalize(WorldToLocal(wi, interaction.ffnormal, interaction.tangent, interaction.bitangent));
				vec3 f = Distribution(interaction.mat, wo, wi);
				wi = LocalToWorld(wi, interaction.ffnormal, interaction.tangent, interaction.bitangent);
				radiance += throughout * f * Li * abs(dot(wi, interaction.ffnormal)) / pdf;
			}
		}

		// Add contribution of each point light source
		for(uint i = 0; i < push_data.spot_light_count; i++)
		{
			vec3 wi;
			float pdf;
			vec3 Li = Sample_Li(spot_lights[i], interaction, rand2(ray_payload.seed), wi, pdf);
			if(Li == vec3(0.0) || pdf == 0)
			{
				continue;
			}

			vec3 dir = normalize(spot_lights[i].position - interaction.position);
			float dist = length(spot_lights[i].position - interaction.position);

			if(Li != vec3(0.0) && Unoccluded(interaction, dir, dist))
			{
				wi = WorldToLocal(wi, interaction.ffnormal, interaction.tangent, interaction.bitangent);
				vec3 f = Distribution(interaction.mat, wo, wi);
				wi = LocalToWorld(wi, interaction.ffnormal, interaction.tangent, interaction.bitangent);
				radiance += throughout * f * Li * abs(dot(wi, interaction.ffnormal)) / pdf;
			}
		}

		// Add contribution of each point light source
		for(uint i = 0; i < push_data.directional_light_count; i++)
		{
			vec3 wi;
			float pdf;
			vec3 Li = Sample_Li(directional_lights[i], interaction, rand2(ray_payload.seed), wi, pdf);
			if(Li == vec3(0.0) || pdf == 0)
			{
				continue;
			}

			if(Li != vec3(0.0) && Unoccluded(interaction, -directional_lights[i].direction, Infinity))
			{
				wi = WorldToLocal(wi, interaction.ffnormal, interaction.tangent, interaction.bitangent);
				vec3 f = Distribution(interaction.mat, wo, wi);
				wi = LocalToWorld(wi, interaction.ffnormal, interaction.tangent, interaction.bitangent);
				radiance += throughout * f * Li * abs(dot(wi, interaction.ffnormal)) / pdf;
			}
		}

		vec3 wi;
		float pdf;
		
		vec3 f = SampleDistribution(interaction.mat, wo, ray_payload.seed, wi, pdf);
		wi = LocalToWorld(wi, interaction.ffnormal, interaction.tangent, interaction.bitangent);

		if(pdf > 0.0 && f != vec3(0.0) && abs(dot(wi, interaction.ffnormal)) != 0.0)
		{
			throughout *= f * abs(dot(wi, interaction.ffnormal)) / pdf;
			ray.direction = wi;
			ray.origin = OffsetRay(interaction.position, dot(wi, interaction.ffnormal) > 0.0 ? interaction.ffnormal : -interaction.ffnormal);
		}
		else
		{
			break;
		}
	}
	
	ivec2 launch_id = ivec2(gl_LaunchIDEXT.x, gl_LaunchSizeEXT.y - gl_LaunchIDEXT.y);

	if(camera_data.frame_num == 0)
	{
		imageStore(Image, launch_id, vec4(radiance, 1.0));
	}
	else
	{
		vec3 prev_color = imageLoad(PrevImage, launch_id).rgb;
		vec3 accumulated_color = vec3(0.0);
		
		if((isnan(prev_color.x) && isnan(prev_color.y) && isnan(prev_color.z)))
		{
			accumulated_color = radiance;
		}
		else
		{
			accumulated_color =mix(prev_color, radiance, 1.0 / float(camera_data.frame_num));
		}

		imageStore(Image, launch_id, vec4(accumulated_color, 1.0));
	}
}
