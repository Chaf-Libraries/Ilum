#version 450

layout (local_size_x = 32, local_size_y = 32) in;

layout (binding = 0, rgba16f) uniform image2D Image;

layout(push_constant) uniform PushBlock{
	uvec2 extent;
    float exposure;
    float gamma;
}push_data;

// From http://filmicgames.com/archives/75
vec3 uncharted2Tonemap(vec3 x)
{
	float A = 0.15;
	float B = 0.50;
	float C = 0.10;
	float D = 0.20;
	float E = 0.02;
	float F = 0.30;
	return ((x*(A*x+C*B)+D*E)/(x*(A*x+B)+D*F))-E/F;
}

void main()
{
	if(all(lessThan(gl_GlobalInvocationID.xy, push_data.extent)))
	{
		vec4 color = imageLoad(Image, ivec2(gl_GlobalInvocationID.xy));

		color.xyz = uncharted2Tonemap(color.xyz * push_data.exposure);
		color.xyz = color.xyz * (1.0 / uncharted2Tonemap(vec3(push_data.gamma)));	
		// Gamma correction
		color.xyz = pow(color.xyz, vec3(1.0 / 2.2));

		imageStore(Image, ivec2(gl_GlobalInvocationID.xy), color);
	}
}