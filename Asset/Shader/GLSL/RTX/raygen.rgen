#version 460
#extension GL_EXT_ray_tracing : enable

layout(binding = 0, set = 0) uniform accelerationStructureEXT topLevelAS;
layout(binding = 1, set = 0, rgba16f) uniform image2D image;
layout(binding = 2, set = 0) uniform CameraProperties 
{
    mat4 view_projection;
	mat4 last_view_projection;
	mat4 view_inverse;
	mat4 projection_inverse;
	vec4 frustum[6];
	vec3 position;
} cam;

layout(location = 0) rayPayloadEXT vec3 hitValue;

void main() 
{
	const vec2 pixelCenter = vec2(gl_LaunchIDEXT.xy) + vec2(0.5);
	vec2 inUV = pixelCenter / vec2(gl_LaunchSizeEXT.xy);
	inUV.y = 1.0 - inUV.y;
	vec2 d = inUV * 2.0 - 1.0;

	vec4 origin = cam.view_inverse * vec4(0 ,0 ,0 ,1);
	vec4 target = cam.projection_inverse * vec4(d.x, d.y, 1, 1) ;
	vec4 direction = cam.view_inverse * vec4(normalize(target.xyz), 0) ;

	float tmin = 0.001;
	float tmax = 10000.0;

    hitValue = vec3(0.0);

    traceRayEXT(topLevelAS, gl_RayFlagsOpaqueEXT, 0xff, 0, 0, 0, origin.xyz, tmin, direction.xyz, tmax, 0);

	imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(hitValue, 1.0));
}
